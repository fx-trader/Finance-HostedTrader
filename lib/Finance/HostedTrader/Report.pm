package Finance::HostedTrader::Report;
# ABSTRACT: Finance::HostedTrader::Report - Report object

=head1 SYNOPSIS

    use Finance::HostedTrader::Report;
    my $report = Finance::HostedTrader::Report->new(
                    account => $account,
                    systemTrader  => $systemTrader,
                );
    print $report->openPositions;
    print $report->systemEntryExit;

=cut

use Moose;
use Moose::Util::TypeConstraints;

use Params::Validate qw(:all);

=attr C<account>

An instance of L<Finance::HostedTrader::Account>

=cut
has account => (
    is     => 'ro',
    isa    => 'Finance::HostedTrader::Account',
    required=>1,
);

=attr C<systemTrader>

An instance of L<Finance::HostedTrader::Trader>

=cut
has systemTrader => (
    is      => 'ro',
    isa     => 'Finance::HostedTrader::Trader',
    required=> 1,
);

=attr C<format>

=cut
enum 'enumFormat' => qw(text html);
has format => (
    is      => 'rw',
    isa     => 'enumFormat',
    required=> 1,
    default => 'text'
);


=method C<openPositions>


=cut

sub openPositions {
    my $self = shift;
    my $account = $self->account;
    my $systemTrader = $self->systemTrader;
    my $positions = $account->getPositions();

    my $t = $self->_table_factory( format=> $self->format, headingText => 'Open Positions', cols => ['Symbol', 'Open Date','Size','Entry','Current','Exit','PL','%','Risk','%'] );
    my $balance = $account->balance;
    my $nav = $account->getNav;

    foreach my $symbol (keys %$positions) {
    my $position = $positions->{$symbol};

    foreach my $trade (@{ $position->getOpenTradeList }) {
        my $direction = $trade->direction;
        my $stopLoss = $systemTrader->getExitValue($trade->symbol, $direction);
        my $marketPrice = ($direction eq 'short' ? $account->getAsk($trade->symbol) : $account->getBid($trade->symbol));
        my $baseCurrencyPL = $trade->pl;
        my $percentPL = sprintf "%.2f", 100 * $baseCurrencyPL / $balance;
        my $currentExit = $systemTrader->getExitValue($symbol, $direction);
        my $amountAtRisk = -1*$trade->navAtRisk($account, $stopLoss);

        $t->addRow(
            $trade->symbol,
            $trade->openDate,
            $trade->size,
            $trade->openPrice,
            $marketPrice,
            $currentExit,
            sprintf('%.2f', $baseCurrencyPL),
            $percentPL,
            sprintf('%.2f',$amountAtRisk),
            sprintf('%.2f',100 * $amountAtRisk / $nav)
        );
    }
    }
    return $t;
}

=method C<systemEntryExit>
=cut
sub systemEntryExit {
    my $self = shift;
    my $account = $self->account;
    my $systemTrader = $self->systemTrader;

    my $t = $self->_table_factory( format => $self->format, headingText => $systemTrader->system->name, cols => ['Symbol','Market','Entry','Exit','Direction', 'Worst Case', '%']);

    foreach my $direction (qw /long short/) {
        foreach my $symbol (@{$systemTrader->system->symbols($direction)}) {
            my $currentExit = $systemTrader->getExitValue($symbol, $direction);
            my $currentEntry = $systemTrader->getEntryValue($symbol, $direction) || 'N/A';
            my $amountAtRisk = -1*$systemTrader->amountAtRisk($account->getPosition($symbol));

            $t->addRow( $symbol, 
                        ($direction eq 'long' ? $account->getAsk($symbol) : $account->getBid($symbol)),
                        $currentEntry,
                        $currentExit,
                        $direction,
                        sprintf('%.2f',$amountAtRisk),
                        sprintf('%.2f',100 * $amountAtRisk / $account->getNav)
            );
        }
    }
    return $t;
}

sub _table_factory {
    my $self = shift;
    my %args = validate( @_, {
        format          => 1,
        headingText    => { type => SCALAR, default => undef },
        cols            => { type => ARRAYREF }
    });

    my $t;

    if ($args{format} eq 'text') {
        require Text::ASCIITable;
        $t = Text::ASCIITable->new( { headingText => $args{headingText} } );
        $t->setCols(@{ $args{cols}} );
    } elsif ($args{format} eq 'html') {
        require HTML::Table;
        $t = HTML::Table->new(-head => $args{cols});
    } else {
        die("unknown format: $args{format}");
    }

    return $t;
}

__PACKAGE__->meta->make_immutable;
1;

=head1 SEE ALSO

L<Finance::HostedTrader::Account>
L<Finance::HostedTrader::Trade>

=cut
